.Object Name: pages_home
*    Version: 0001
*
*===============================================================================
@0001:() . Entry point for ICE Service Handler
@     if <signedin> eq true . ; gto 0010 .
@. ---- TODO Make form values inputable from a table some how ----
@     def,,lin1 <t>i2,<micropost> gto lin+2 .
@     ldv <micropost>s0 .
@.
@.   <img src="http://www.gravatar.com/avatar/205e460b479e2e5b48aec07710c08d50" />
@.
@.[script]
@.
@. // Create the feed function that will refresh the posts on the page
@. var feed = function () {
@.   $.post(document.referrer);
@. }
@.
@. // Call the feed function every 60 seconds to refresh
@. setInterval( feed, 5000 );
@.
@.[/script]
@.
@. ---- Based on the <category>,<service>, and <action> the following ----
@. ---- <f_...> variables will be loaded with the correct values ---------
@     call,Shared_Form_Parts (<category>,<service>,<action>,\
                              <f_action>s500,<f_meth>h8,<f_class>s60,<f_id>s40) .
  [table class="front" summary="For signed-in users"]
    [tr]
      [td class="main"]
        [h1 class="micropost"]What''s up?[/h1]
          [!-- Move to partial --]
          [form action="<f_action>" method="<f_meth>" class="<f_class>" id="<f_id>" /]
          [div class="field"]
          [textarea style="height:100px;" id="micropost_content" name="micropost"][/textarea]
          [/div]
          [div class="actions"]
            [input type="submit" value="Submit"]
          [/div]
          [/form]
      [!-- Move to partial as well --]
@     lzr,-5,0009 ,,<hln>i7 .                                         Check if micropost feed exists
          [table class="microposts" summary="Status Feed"]
@0005:inc <hln> rdl,-5,<hln>,0008 'id','postid','content','datecreated','timecreated' \
                  <feeduser>h,<postid>h,<feedcontent>s,<date>h,<time>h .
@     call,Models_User 0090 (<feeduser>,<name>s50) .                                  Get users name
            [tr]
              [td class="micropost"]
                [span class="user"]
                  [a href=# title="<name>(p)"] <name>(p) [/a]
                [/span]
                [span class="content"] <feedcontent>(p) [/span][br]
                [span class="timestamp"]
@     call,Shared_Time_Ago (<date>,<time>,<timeInWords>s40) .
                  Posted <timeInWords>(p).
                [/span]
              [/td]
              [td]
@     if <id> eq <feeduser> . ; gto lin+2 .
                [a href="<GUrlCat>microposts&action=destroy&postid=<postid>"]delete[/a]
              [/td]
            [/tr]
@     gto 0005 .                                                                       Get next post
@0008:.Done
          [/table]
@0009:.
      [/td]
      [td class="sidebar round"]
        &nbsp;
@.      [%= render 'shared/user_info' %]
      [/td]
    [/tr]
  [/table]
@     gto 0099 .
@0010:.
  [h1]Sample App[/h1]
  [p]
    This is the home page for the [a href="http://railstutorial.org/"]Ruby on Rails Tutorial[/a]
    sample application.
  [/p]
  [a href="<gurlcat>users&action=new" class="signup_button round"]Sign up now![/a]
@0099:esr . Return to ICE Service Handler
