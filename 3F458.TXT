.Object Name: users
*    Version: 0001
*
*===============================================================================
:INCLUDE,ECAB$,B,0002 .
@.
@. -----------------------------------------------------------------------------------------------
@. -   M A P P E R  M V S in Theory -                                                            -
@. -                                                                                             -
@. -   MODEL: user                         ------------                                          -
@. -   VIEW: users_(see below)             - Browser  -                                          -
@. -   SERVICE: users                      -          -                                          -
@. -                                       - From Web -                                          -
@. -                                       ------------                                          -
@. -                                          |   / \                                            -
@. -                                          |    |                                             -
@. -                                         (1)  (6)                                            -
@. -                                          |    |                                             -
@. -                                         \ /   |                                             -
@. -       ------------------         ---------------------         ----------------------       -
@. -       - MODEL:         -         - SERVICE:          -         - VIEW:              -       -
@. -       -                - <-(2)-- -                   - <-(5)-- -                    -       -
@. -       - Business Logic - --(3)-> - Application Logic - --(4)-> - Presentation Logic -       -
@. -       -                -         -                   -         -                    -       -
@. -       ------------------         ---------------------         ----------------------       -
@. -                                                                                             -
@. - P R O C E S S:                                                                              -
@. -                                                                                             -
@. - (1) - Browser issues request to SERVICE.                                                    -
@. - (2) - The MODEL is called from the SERVICE to handle the business logic and data.           -
@. - (3) - The MODEL returns to the SERVICE.                                                     -
@. - (4) - The SERVICE renders the VIEW.                                                         -
@. - (5) - The VIEW output is returned to the SERVICE.                                           -
@. - (6) - The SERVICE returns to ICESVCHND with the output of the page.                         -
@. -                                                                                             -
@. - V I E W S:                                                                                  -
@. -                                                                                             -
@. - ( 9d458) - users_new        ../Category=Sample_App&Service=Users&Action=Create              -
@. - (10d458) - users_show       ../Category=Sample_App&Service=Users&Action=Show                -
@. - (11d458) - users_index      ../Category=Sample_App&Service=Users&Action=Index               -
@. - (12d458) - users_edit       ../Category=Sample_App&Service=Users&Action=Edit                -
@. -                                                                                             -
@. - A C T I O N S:                                                                              -
@. -       .              .       .                   .                                          -
@. - HTTP  .URL           .Action .Named route        .Purpose                                   -
@. - ======.==============.=======.===================.=====================================     -
@. - GET    /users         index   users_path          page to list all users                    -
@. - GET    /users/1       show    user_path(1)        page to show user with id 1               -
@. - GET    /users/new     new     new_user_path       page to make a new user (signup)          -
@. - POST   /users         create  users_path          create a new user                         -
@. - GET    /users/1/edit  edit    edit_user_path(1)   page to edit user with id 1               -
@. - PUT    /users/1       update  user_path(1)        update user with id 1                     -
@. - DELETE /users/1       destroy user_path(1)        delete user with id 1                     -
@. -                                                                                             -
@. -----------------------------------------------------------------------------------------------
@.
@     ldv <run>a1=Y .                                                          using-rpx-to-execute?
@     rnm,ecab$,g,erpt$ -8 .
@     gto lin+3 .
@0001:() .                                                       Entry point for ICE Service Handler
@     rnm -8 ldv <run>h1=n if ecab$ LT 298 . ; rep,-8,ecab$,g,erpt$ .
@     rsr,-8 001 .                     set values from browser input            Get Input Parameters
@.
@. ---- Initialize the application variables ----
@     rsr,Shared_AppConfig 0001 .
@.
@. ---- Skip ahead if <action> undefined ----
@     def,p,0005 <t>i2,<action> if <t> eq 0,(0199) .                     Skip to View if Not Defined
@.
@. ---- Filters ----
@.    These are used to call certain functions before the action is executed
@.    And to restrict certain actions based on the type of user that is signed in
@.ew  if <action> ne show,new,create call,Helper_Sessions ('authenticate',<redirect>) .
@.ew  if <action> eq edit,update     call,Helper_Sessions ('correct_user') .
@.ew  if <action> eq destroy         call,Helper_Sessions ('admin_user') .
@     if <redirect> eq true . ; gto 0008 .
@     rsr Service_Execute 0005 .
@0005:.
@     csr .                                                         Clear the rsr to get normal flow
@. ---- Defined Actions ----
@0008:if <action> eq 'index       ',(0010) . ; .
@     if <action> eq 'show        ',(0020) . ; .
@     if <action> eq 'otheruser   ',(0025) . ; .
@     if <action> eq 'followers   ',(0030) . ; .
@     if <action> eq 'following   ',(0040) . ; .
@     if <action> eq 'new         ',(0050) . ; .
@     if <action> eq 'create      ',(0060) . ; .
@     if <action> eq 'edit        ',(0070) . ; .
@     if <action> eq 'update      ',(0080) . ; .
@     if <action> eq 'destroy     ',(0090) . ; .
@.
@. ---- Action :: index ----
@0010:.
@     ldv <title>s50='All Users' .
@     srh,DB_Users 'dl( )n@' 'id' 	,@@@@@@@@ .
@     rnm -1 .
@     gto 0199 .
@.
@. ---- Action :: show  ----
@0020:.
@     rsr,Models_User 0020 .                                                   Find User By Id
@     ldv <title>s50=<name> .
@     gto 0199 .
@.
@. ---- Action :: otheruser ----
@0025:.
@     rsr,Models_User 0025 .                                                   Find User By Id
@     ldv <title>s50=<name> .
@     gto 0199 .
@.
@. ---- Action :: following ----
@0030:.
@.
@. ---- Action :: followers ----
@0040:.
@.
@. ---- Action :: new ----
@0050:.
@     ldv <title>s20='Sign Up' .
@     gto 0199 .
@.
@. ---- Action :: create ----
@0060:.
@     rsr,Models_User 0001 .                                        Validation for user sign up page
@     if <validation> eq success . ; gto 0065 .                                      All edits good?
@     rsr,Models_User 0060 .                                                         Create new user
@     ldv <title>s20='Show' .
@     ldv <redirect>h4=true,<signin>h4=true .                                Redirect to show action
@     gto 0199 .
@0065:.
@     ldv <action>h3=new .                                           Render the new action if failed
@     gto 0050 .
@.
@. ---- Action :: edit ----
@0070:.
@     rsr,Models_User 0020 .                                                         Find user by id
@     ldv <title>s50='Edit user' .
@     gto 0199 .
@.
@. ---- Action :: update ----
@0080:.
@     rsr,Models_User 0001 .                                           Validation for user edit page
@     if <validation> eq success . ; gto 0085 .                                      All edits good?
@     rsr,Models_User 0080 .                                                    Update existing user
@     ldv <redirect>h4=true .                                           Re-direct to users show page
@     gto 0199 .
@0085:.
@     ldv <title>s50='Edit user' .
@     ldv <action>h4=edit .
@     gto 0199 .
@.
@. ---- Action :: destroy ----
@0090:.
@.
@. ---- Call the application view including headers and footers ----
@0199:.
@     rsr,View_Application .
@     if <run> eq y dsx,-0 . ; .                                                End HTML Output Area
@     return                                                 Return to ICE Service Handler .
